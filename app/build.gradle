apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: 'com.cookpad.android.licensetools'

// Load keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

ext {
  supportLibraryVersion = '25.3.1'
  retrofitVersion = '2.1.0'
  okhttpVersion = '3.7.0'
  rxVersion = '2.1.0'
}

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.2"

  defaultConfig {
    applicationId "com.futabooo.android.booklife"
    minSdkVersion 21
    targetSdkVersion 25
    versionCode 11
    versionName "1.1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    release {
      try {
        storeFile file(keystoreProperties['storeFile'])
        storePassword keystoreProperties['storePassword']
        keyAlias keystoreProperties['keyAlias']
        keyPassword keystoreProperties['keyPassword']
      } catch (ex) {
        throw new InvalidUserDataException(
            "You should define RELEASE_KEYSTORE_PASSWORD and " + "RELEASE_KEY_PASSWORD in gradle.properties.")
      }
    }
    debug {
      storeFile rootProject.file("debug.keystore")
      storePassword "android"
      keyAlias "androiddebugkey"
      keyPassword "android"
    }
  }

  buildTypes {
    release {
      minifyEnabled true
      debuggable false
      zipAlignEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField "boolean", "USE_CRASHLYTICS", "true"
      ext.enableCrashlytics = true
    }
    debug {
      debuggable true
      zipAlignEnabled false
      signingConfig signingConfigs.debug

      buildConfigField "boolean", "USE_CRASHLYTICS", "false"
      ext.enableCrashlytics = false
    }

    lintOptions { checkReleaseBuilds false }
  }

  dataBinding {
    enabled = true
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

repositories {
  mavenCentral()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  maven { url "https://maven.fabric.io/public" }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

  compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
    transitive = true
  }
  compile('com.crashlytics.sdk.android:answers:1.3.13@aar') {
    transitive = true
  }

  compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
  compile "com.android.support:design:${supportLibraryVersion}"
  kapt 'com.android.databinding:compiler:2.5.0-alpha-preview-02'

  // jsoup HTML parser library @ http://jsoup.org/
  compile 'org.jsoup:jsoup:1.10.2'

  // http client
  compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
  compile "com.squareup.okhttp3:okhttp-urlconnection:${okhttpVersion}"
  compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
  compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
  compile "com.squareup.retrofit2:converter-scalars:${retrofitVersion}"
  compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
  compile 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

  compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
  compile "io.reactivex.rxjava2:rxjava:${rxVersion}"
  compile "io.reactivex.rxjava2:rxkotlin:${rxVersion}"

  // data store
  compile 'com.kazakago.cryptore:cryptore:1.0.0'

  // dagger2
  compile 'com.google.dagger:dagger:2.11'
  kapt 'com.google.dagger:dagger-compiler:2.11'

  // images
  compile 'com.github.bumptech.glide:glide:3.7.0'
  compile 'jp.wasabeef:glide-transformations:2.0.1'

  // views
  compile 'com.roughike:bottom-bar:2.1.1'
  compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
  compile "com.android.support:cardview-v7:${supportLibraryVersion}"
  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  compile 'com.github.rafakob:DrawMe:0.1.6'
  compile 'com.ogaclejapan.arclayout:library:1.1.0@aar'

  // util
  compile 'com.jakewharton.timber:timber:4.5.1'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  testCompile 'junit:junit:4.12'
}

afterEvaluate {
  // register fabric.properties creation task
  initCrashlyticsPropertiesIfNeeded()
}

def initCrashlyticsPropertiesIfNeeded() {
  def propertiesFile = file('fabric.properties')
  if (!propertiesFile.exists()) {
    // create fabric.properties file using gradle ant task
    def commentMessage = "This is autogenerated crashlytics property from system environment to prevent key to be committed to source control."
    ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
      entry(key: "apiSecret", value: crashlyticsApiSecret)
      entry(key: "apiKey", value: crashlyticsApiKey)
    }
  }
}
